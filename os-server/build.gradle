plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'checkstyle'
    id "jacoco"
    id 'idea'
}

group 'app.open-school'
version '1.0-SNAPSHOT'

dependencies {
    runtimeOnly project(':os-ui')
    // spring dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.auth0:java-jwt:3.18.3'

    // db dependencies
    runtimeOnly 'mysql:mysql-connector-java'
    implementation('com.h2database:h2')
    implementation 'org.flywaydb:flyway-core:8.5.4'
    implementation 'org.flywaydb:flyway-mysql:8.4.4'

    //testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-inline:4.6.1'

    //open api
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.6'

    // apache commons-lang3
    implementation 'org.apache.commons:commons-lang3'

    //email support
    implementation "org.springframework.boot:spring-boot-starter-mail"

    //template engines
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"

    //logging
    implementation 'ch.qos.logback:logback-classic:1.2.11'

    //aws
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.1000')
    implementation 'com.amazonaws:aws-java-sdk-s3'
}

test {
    useJUnitPlatform()
    environment "SPRING_PROFILES_ACTIVE", "test"
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

jar {
    enabled = false
}

checkstyle {
    toolVersion '8.40'
    maxWarnings = 0
    ignoreFailures false
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}